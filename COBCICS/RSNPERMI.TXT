      *************************************************************
      *   ENCAPSULADOR TABLA PERMISOS
      *   RUTINA QUE EJECUTA LAS SENTENCIAS DE LA BASE DE DATOS A
      *   PARTIR DE UN TIPO Y SUBTIPO DE OPERACION QUE ENVIA
      *   EL PROGRAMA QUE LA INVOCA.
      *   TIPO 1: CONSULTA - SELECT
      *      SUBTIPO 1: CONSULTA POR TRANSACCION Y DNI
      *        2: ADICION - INSERT
      *        3: BORRAR - DELETE
      *        4: MODIFICAR - UPDATE
      *************************************************************
       IDENTIFICATION DIVISION.
         PROGRAM-ID. RSNPERMI.
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
      *===============================================================
      * MODSQL - DEFINICION TABLA PERMISOS SQL Y COBOL
           EXEC SQL
                INCLUDE PERMISOS
           END-EXEC.
      *===============================================================
      * MODSQL - SE TIENE QUE UTILIZAR SQLCA
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.

      *============================================================
       LINKAGE SECTION.
       01  DFHCOMMAREA           PIC X(52).
       01  AREA-LINK          REDEFINES DFHCOMMAREA.
           05 LK-PM-TIPO-OPE     PIC 9(01).
           05 LK-PM-SUBTIPO-OPE  PIC 9(02).
           05 LK-PM-TRAN         PIC X(04).
           05 LK-PM-DNI          PIC X(09).
           05 LK-PM-IDPERFIL     PIC X(03).
           05 LK-PM-NROACCESOS   PIC S9(9) USAGE COMP.
           05 LK-PM-FECHAUL      PIC X(10).
           05 LK-PM-HORAUL       PIC X(08).
           05 LK-PM-COD-RET      PIC 9(02).
           05 LK-PM-SQLCODE      PIC S9(9).

      *=================================================================
       PROCEDURE DIVISION USING DFHCOMMAREA.
      *=================================================================
      ******************************************************************
      * 0000-RAIZ
      ******************************************************************
       0000-RAIZ.
      *    MODSQL EXCEPCIONES SQL DB2
           EXEC SQL
                WHENEVER  SQLERROR    CONTINUE
           END-EXEC

           EXEC SQL
                WHENEVER  SQLWARNING  CONTINUE
           END-EXEC

           EXEC SQL
                WHENEVER  NOT FOUND   CONTINUE
           END-EXEC

      *    PERFORM 0100-INICIO
           PERFORM 0200-EJECUTAR-OPERACION
           PERFORM 0900-FIN
           .
      ******************************************************************
      * 0100-INICIO
      ******************************************************************
      *0100-INICIO.
      *.
      ******************************************************************
      * 0200-EJECUTAR-OPERACION
      ******************************************************************
       0200-EJECUTAR-OPERACION.
           EVALUATE LK-PM-TIPO-OPE
              WHEN 1
                 PERFORM 0210-CONSULTAR-PERMISO
              WHEN 2
                 PERFORM 0220-INSERTAR-PERMISO
              WHEN 3
                 PERFORM 0230-BORRAR-PERMISO
              WHEN 4
                 PERFORM 0240-MODIFICAR-PERMISO
              WHEN OTHER
      *          DISPLAY "OPCION NO IMPLEMENTADA"
                 MOVE 9                        TO LK-PM-COD-RET
           END-EVALUATE
           .
      **************************************************************
      * 0210-CONSULTAR-PERMISO
      **************************************************************
       0210-CONSULTAR-PERMISO.
           MOVE 1          TO LK-PM-COD-RET
           MOVE LK-PM-TRAN TO PM-TRAN
           MOVE LK-PM-DNI  TO PM-DNI
      *
           EXEC SQL
             SELECT
                IDPERFIL, NROACCESOS, FECHAUL, HORAUL
             INTO
                :PM-IDPERFIL, :PM-NROACCESOS, :PM-FECHAUL, :PM-HORAUL
             FROM PERMISOS
             WHERE TRAN = :PM-TRAN  AND  DNI = :PM-DNI
           END-EXEC

           IF SQLCODE = 0 THEN
              MOVE 0             TO  LK-PM-COD-RET
              MOVE PM-IDPERFIL   TO  LK-PM-IDPERFIL
              MOVE PM-NROACCESOS TO  LK-PM-NROACCESOS
              MOVE PM-FECHAUL    TO  LK-PM-FECHAUL
              MOVE PM-HORAUL     TO  LK-PM-HORAUL
           ELSE
              IF SQLCODE = +100 THEN
                 MOVE 1 TO  LK-PM-COD-RET
              ELSE
                 MOVE 2 TO  LK-PM-COD-RET
                 PERFORM 0900-ERROR-DB2
              END-IF
           END-IF
           .
      ***************************************************
      * INSERTA LOS DATOS EN LA TABLA                   *
      ***************************************************
       0220-INSERTAR-PERMISO.
           MOVE LK-PM-TRAN       TO  PM-TRAN
           MOVE LK-PM-DNI        TO  PM-DNI
           MOVE LK-PM-IDPERFIL TO PM-IDPERFIL
           MOVE LK-PM-NROACCESOS TO PM-NROACCESOS
           MOVE LK-PM-FECHAUL TO     PM-FECHAUL
           MOVE LK-PM-HORAUL     TO  PM-HORAUL

           EXEC SQL
             INSERT INTO PERMISOS
                (
                 TRAN, DNI, IDPERFIL, NROACCESOS, FECHAUL, HORAUL
                )
             VALUES
                (
                 :PM-TRAN, :PM-DNI, :PM-IDPERFIL,
                 :PM-NROACCESOS, :PM-FECHAUL, :PM-HORAUL
                )
           END-EXEC

           IF SQLCODE = 0
             MOVE 0         TO LK-PM-COD-RET
           ELSE
             IF SQLCODE = +100 THEN
                MOVE 1      TO  LK-PM-COD-RET
             ELSE
                MOVE 2      TO  LK-PM-COD-RET
                PERFORM 0900-ERROR-DB2
             END-IF
           END-IF
           .
      ***************************************************
      * BORRA UN REGISTRO CON EL NUMERO DE LA CLAVE     *
      ***************************************************
       0230-BORRAR-PERMISO.
           MOVE LK-PM-TRAN       TO  PM-TRAN
           MOVE LK-PM-DNI        TO  PM-DNI

           EXEC SQL
             DELETE FROM PERMISOS
               WHERE TRAN = :PM-TRAN  AND  DNI = :PM-DNI
           END-EXEC

           IF SQLCODE = 0
             MOVE 0         TO LK-PM-COD-RET
           ELSE
             IF SQLCODE = +100 THEN
                MOVE 1      TO  LK-PM-COD-RET
             ELSE
                MOVE 2      TO  LK-PM-COD-RET
                PERFORM 0900-ERROR-DB2
             END-IF
           END-IF
           .
      ***************************************************
      * ACTUALIZA LOS DATOS EN LA TABLA                 *
      ***************************************************
       0240-MODIFICAR-PERMISO.
           MOVE LK-PM-TRAN       TO  PM-TRAN
           MOVE LK-PM-DNI        TO  PM-DNI
           MOVE LK-PM-IDPERFIL TO PM-IDPERFIL
           MOVE LK-PM-NROACCESOS TO PM-NROACCESOS
           MOVE LK-PM-FECHAUL TO     PM-FECHAUL
           MOVE LK-PM-HORAUL     TO  PM-HORAUL

           EXEC SQL
              UPDATE PERMISOS
              SET IDPERFIL    = :PM-IDPERFIL,
                  NROACCESOS  = :PM-NROACCESOS,
                  FECHAUL     = :PM-FECHAUL,
                  HORAUL      = :PM-HORAUL
              WHERE TRAN = :PM-TRAN  AND  DNI = :PM-DNI
           END-EXEC

           IF SQLCODE = 0
             MOVE 0         TO LK-PM-COD-RET
           ELSE
             IF SQLCODE = +100 THEN
                MOVE 1      TO  LK-PM-COD-RET
             ELSE
                MOVE 2      TO  LK-PM-COD-RET
                PERFORM 0900-ERROR-DB2
             END-IF
           END-IF
           .
      ******************************************************************
      * 0900-ERROR-DB2
      ******************************************************************
       0900-ERROR-DB2.
           MOVE SQLCODE        TO LK-PM-SQLCODE
           .
      ******************************************************************
      * FIN SESION
      ******************************************************************
       0900-FIN.
           GOBACK
           .
      ******************************************************************
