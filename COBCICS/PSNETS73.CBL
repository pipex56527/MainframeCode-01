000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. PSNETS73.
000300 AUTHOR. LUIS F VELASQUEZ T.
000400 DATE-WRITTEN. 21/09/2024.
000500 DATE-COMPILED.
000595****************************************************
000600* SIMULA ENCAPSULADOR DE USUARIOS                  *
000620* GENERA UNA TABLA EN MEMORIA CON LOS  DATOS       *
000630* DE 6 ESTUDIANTES                                 *
000640* ES LLAMADO MEDIANTE EL COMANDO LINK DE CICS      *
000650* RECIBE EL DNI DEL ESTUDIANTE A CONSULTAR         *
000660* REGRESA EL INDICADOR DE SI LO ENCONTRO O NO      *
000670* Y SI LO ENCUENTRA DEVUELVE EL NOMBRE, APELLIDO,  *
000680* EL INDICADOR DE QUE LO ENCONTRO.                 *
000690*                                                  *
000695*                             .                    *
000696****************************************************
000700 ENVIRONMENT DIVISION.
000800 CONFIGURATION SECTION.
000900 SPECIAL-NAMES.
001000 INPUT-OUTPUT SECTION.
001100 DATA DIVISION.
001200 WORKING-STORAGE SECTION.
001300* COPY AREAS PARA FECHAS
001400* COPY UFECHAS.
001500 01  WS-NOMBRES-MESES.
001600     03 WS-NOM-MESES
001700          PIC X(36) VALUE
001800       "ENEFEBMARABRMAYJUNJULAGOSEPOCTNOVDIC".
001900     03 WS-NOM-MES REDEFINES WS-NOM-MESES
002000          PIC XXX OCCURS 12 TIMES INDEXED BY IX-MES.
002100 01  WS-CURRENT-DATE-FIELDS.
002200     05 WS-CURRENT-DATE.
002300       10 WS-CURRENT-YEAR     PIC 9999.
002400       10 WS-CURRENT-MONTH    PIC 99.
002500       10 WS-CURRENT-DAY      PIC 99.
002600     05 WS-CURRENT-TIME.
002700       10 WS-CURRENT-HH       PIC 99.
002800       10 WS-CURRENT-MM       PIC 99.
002900       10 WS-CURRENT-SS       PIC 99.
003000       10 WS-CURRENT-CC       PIC 99.
003100 01  WS-FECHA-AAAA-MM-DD.
003200       10 WS-AAAA             PIC 9999.
003300       10 FILLER              PIC X VALUE "-".
003400       10 WS-MM               PIC 99.
003500       10 FILLER              PIC X VALUE "-".
003600       10 WS-DD               PIC 99.
003700 01  WS-HORA-MINUTOS-SEGUNDOS.
003800       10 WS-HH-F1            PIC 99.
003900       10 FILLER              PIC X VALUE ".".
004000       10 WS-MM-F1            PIC 99.
004100       10 FILLER              PIC X VALUE ":".
004200       10 WS-SS-F1            PIC 99.
004300       10 FILLER              PIC X VALUE ":".
004400       10 WS-CC-F1            PIC 99.
004500 01  WS-FECHA-DD-MMM-AA.
004600       10 WS-DD-F2            PIC 99.
004700       10 FILLER              PIC X VALUE "-".
004800       10 WS-MM-F2            PIC XXX.
004900       10 FILLER              PIC X VALUE "-".
005000       10 WS-AAAA-F2          PIC 9999.
005100
009300 01 DATOS-PRUEBA.
009400    03 WS-TAB-ESTUDIANTES.
009500       05 WS-TAB-ESTUDIANTE-1.
009600          07 FILLER PIC X(9)  VALUE "52056163".
010500          07 FILLER PIC X(20) VALUE "LUZ MERY".
010600          07 FILLER PIC X(20) VALUE "ALARCON MEJIA".
009900       05 WS-TAB-ESTUDIANTE-2.
010000          07 FILLER PIC X(9)  VALUE "52057782".
010900          07 FILLER PIC X(20) VALUE "MONICA LILIANA".
011000          07 FILLER PIC X(20) VALUE "RODRIGUEZ RODRIGUEZ".
010300       05 WS-TAB-ESTUDIANTE-3.
010400          07 FILLER PIC X(9)  VALUE "52263924".
011300          07 FILLER PIC X(20) VALUE "ROSA CANDIDA".
011400          07 FILLER PIC X(20) VALUE "OJEDA".
010700       05 WS-TAB-ESTUDIANTE-4.
010800          07 FILLER PIC X(9)  VALUE "52438489".
010100          07 FILLER PIC X(20) VALUE "DORY CONSTANZA".
010200          07 FILLER PIC X(20) VALUE "SANCHEZ ARAGON".
011100       05 WS-TAB-ESTUDIANTE-5.
011200          07 FILLER PIC X(9)  VALUE "52825799".
011700          07 FILLER PIC X(20) VALUE "MAGDA MILENA".
011800          07 FILLER PIC X(20) VALUE "RODRIGUEZ".
011500       05 WS-TAB-ESTUDIANTE-6.
011600          07 FILLER PIC X(9)  VALUE "9533075".
009700          07 FILLER PIC X(20) VALUE "FLAVIO NESTOR".
009800          07 FILLER PIC X(20) VALUE "PATINO CORREA".
011900    03 WS-TAB-ESTUDIANTES-R REDEFINES WS-TAB-ESTUDIANTES.
011950       05 DNI-DNI OCCURS 6 TIMES INDEXED BY IX-DNI.
012000          07 DNI-ESTUD  PIC X(9).
012100          07 DNI-NOMEST PIC X(20).
012200          07 DNI-APEEST PIC X(20).
013800* COPY WSDEBDIS.
013900 01  WS-SW-DEBUG.
014000     05 W-SW-DEBUG    PIC X VALUE "S".
014100 01  WS-DEBDIS-MSG.
014200     05 W-TIME-DIS    PIC X(11).
014300     05 FILLER        PIC X VALUE SPACES.
014400     05 W-PARRAFO     PIC X(10) VALUE SPACES.
014500     05 FILLER        PIC X VALUE SPACES.
014600     05 W-CAMPO       PIC X(10) VALUE SPACES.
014700     05 FILLER        PIC X VALUE "=".
014800     05 W-VALOR       PIC X(20).
014900     05 FILLER        PIC X VALUE SPACES.
015000     05 W-CAMPO1      PIC X(10) VALUE SPACES.
015100     05 FILLER        PIC X VALUE "=".
015200     05 W-VALOR1      PIC X(20).
015300     05 FILLER        PIC X VALUE SPACES.
015400     05 W-FECHA-DIS   PIC X(10).
015500
015600* AYUDAS DE BMS
015700 COPY DFHAID.
015800 COPY DFHBMSCA.
015900 LINKAGE SECTION.
016000 01  DFHCOMMAREA        PIC X(132).
016010 01  AREA-LINK      REDEFINES DFHCOMMAREA.
016020     05 LK-US-TIPO-OPE        PIC 9.
016030     05 LK-US-SUBTIPO-OPE     PIC 99.
016040     05 LK-US-DNI             PIC X(09).
016050     05 LK-US-TIPOUSUA        PIC X(05).
016060     05 LK-US-CLAVE           PIC X(08).
016070     05 LK-US-NOMBRE          PIC X(20).
016080     05 LK-US-APELLIDOS       PIC X(20).
016082     05 LK-US-DIRECCION       PIC X(35).
016084     05 LK-US-CARGO           PIC X(20).
016086     05 LK-US-ESTADO          PIC X(01).
016090     05 LK-US-COD-RET         PIC 9(02).
016100     05 LK-US-SQLCODE         PIC S9(9).
017300 PROCEDURE DIVISION USING DFHCOMMAREA.
017400 0000-MAIN.
017500     PERFORM 1000-INICIO-DE-PROGRAMA
017600     PERFORM 2000-PROCESO-PETICION
017700     PERFORM 9999-FIN-DE-PROGRAMA
017800     .
017900 1000-INICIO-DE-PROGRAMA.
017950     DISPLAY "073-REC : " LK-US-TIPOUSUA "/" LK-US-DNI "/"
017960              LK-US-TIPO-OPE "/" LK-US-SUBTIPO-OPE
018600     .
018700 2000-PROCESO-PETICION.
019300
019400     PERFORM 3200-CONSULTA-DNI
019600     .
022200* COPY RUTINA FORMATO DE FECHAS
022300* COPY UGETDATE.
022400 3100-GET-DATES.
022500     MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE-FIELDS
022600     MOVE WS-CURRENT-YEAR       TO WS-AAAA
022700     MOVE WS-CURRENT-MONTH      TO WS-MM
022800     MOVE WS-CURRENT-DAY        TO WS-DD
022900     MOVE WS-CURRENT-YEAR       TO WS-AAAA-F2
023000     MOVE WS-CURRENT-DAY        TO WS-DD-F2
023100     SET IX-MES TO WS-CURRENT-MONTH
023200     MOVE WS-NOM-MES (IX-MES) TO WS-MM-F2
023300     MOVE WS-CURRENT-HH         TO WS-HH-F1
023400     MOVE WS-CURRENT-MM         TO WS-MM-F1
023500     MOVE WS-CURRENT-SS         TO WS-SS-F1
023600     MOVE WS-CURRENT-CC         TO WS-CC-F1
023700     .
023800
023900 3200-CONSULTA-DNI.
024000     MOVE "--73-3200-BUSCA-DNI--" TO W-PARRAFO
024300     MOVE "MDNIESI" TO W-CAMPO
024300     MOVE LK-US-DNI TO W-VALOR
024400     PERFORM 9100-DEB-DIS
024500
024600     PERFORM 3100-GET-DATES
024800     MOVE "**NO REGISTRADO **" TO LK-US-NOMBRE
024800     MOVE "**NO REGISTRADO **" TO LK-US-APELLIDOS
024900     MOVE 1 TO LK-US-COD-RET MOVE 100 TO LK-US-SQLCODE
025000     SET IX-DNI TO 1
025100     SEARCH  DNI-DNI
025200         AT END MOVE 1 TO LK-US-COD-RET
025300     WHEN DNI-ESTUD (IX-DNI) = LK-US-DNI
025400        MOVE 0 TO LK-US-COD-RET
025500        MOVE DNI-NOMEST (IX-DNI) TO LK-US-NOMBRE
025600        MOVE DNI-APEEST (IX-DNI) TO LK-US-APELLIDOS
              MOVE 0 TO LK-US-SQLCODE
025900     END-SEARCH
027200     .
040700* COPY PDEBDIS.
040800 9100-DEB-DIS.
040900      PERFORM 3100-GET-DATES
041000      MOVE WS-FECHA-AAAA-MM-DD TO W-FECHA-DIS
041100      MOVE WS-HORA-MINUTOS-SEGUNDOS TO W-TIME-DIS
041200      IF W-SW-DEBUG = "S" THEN
041300         DISPLAY WS-DEBDIS-MSG
041400      END-IF
041600      .
041800 9999-FIN-DE-PROGRAMA.
017950     DISPLAY "073-DEV : " LK-US-TIPOUSUA "/" LK-US-DNI "/"
017960              LK-US-TIPO-OPE "/" LK-US-SUBTIPO-OPE
041900              "/" LK-US-COD-RET "/" LK-US-NOMBRE "/"
042000              " SQL >>" LK-US-SQLCODE
042500     GOBACK
042600     .
