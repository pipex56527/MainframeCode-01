      *************************************************************
      *   ENCAPSULADOR TABLA PERFILES
      *   RUTINA QUE EJECUTA LAS SENTENCIAS DE LA BASE DE DATOS A
      *   PARTIR DE UN TIPO Y SUBTIPO DE OPERACION QUE ENVIA
      *   EL PROGRAMA QUE LA INVOCA.
      *   TIPO 1: CONSULTA - SELECT
      *      SUBTIPO 1: CONSULTA POR TRANSACCION Y IDPERFIL
      *        2: ADICION - INSERT
      *        3: BORRAR - DELETE
      *        4: MODIFICAR - UPDATE
      *************************************************************
       IDENTIFICATION DIVISION.
         PROGRAM-ID. RSNPERFI.
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
      *===============================================================
      * MODSQL - DEFINICION TABLA PERFILES SQL Y COBOL
           EXEC SQL
                INCLUDE PERFILES
           END-EXEC.
      *===============================================================
      * MODSQL - SE TIENE QUE UTILIZAR SQLCA
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.
      *===============================================================
      * MODSQL CAMPOS PARA ERROR DE DB2
       01  DB2-ERR.
           03  DB2-SQLCODE                PIC S9(9).
           03  DB2-SQLCODE-Z              PIC -ZZZZZZZZ9.
           03  DB2-ERROR.
               05  DB2-ERR-MSG            PIC X(50).
               05  DB2-ERR-CODE           PIC X(20).

      *============================================================
       LINKAGE SECTION.
       01  DFHCOMMAREA        PIC X(49).
       01  AREA-LINK          REDEFINES DFHCOMMAREA.
           05 LK-PF-TIPO-OPE     PIC 9(01).
           05 LK-PF-SUBTIPO-OPE  PIC 9(02).
           05 LK-PF-TRAN         PIC X(04).
           05 LK-PF-IDPERFIL     PIC X(03).
           05 LK-PF-NOMBRE       PIC X(20).
           05 LK-PF-CONSUL       PIC X(02).
           05 LK-PF-BORRAR       PIC X(02).
           05 LK-PF-INGRESAR     PIC X(02).
           05 LK-PF-MODIF        PIC X(02).
           05 LK-PF-COD-RET      PIC 9(02).
           05 LK-PF-SQLCODE      PIC S9(9).
      *=================================================================
       PROCEDURE DIVISION USING DFHCOMMAREA.
      *=================================================================
      ******************************************************************
      * 0000-RAIZ
      ******************************************************************
       0000-RAIZ.
      *    MODSQL EXCEPCIONES SQL DB2
           EXEC SQL
                WHENEVER  SQLERROR    CONTINUE
           END-EXEC

           EXEC SQL
                WHENEVER  SQLWARNING  CONTINUE
           END-EXEC

           EXEC SQL
                WHENEVER  NOT FOUND   CONTINUE
           END-EXEC

           PERFORM 0100-INICIO
           PERFORM 0200-EJECUTAR-OPERACION
           PERFORM 0300-FIN
           .
      ******************************************************************
      * 0100-INICIO
      ******************************************************************
       0100-INICIO.
      *    DISPLAY "ENTRO A RUTINA RSNPERFI"
      *    DISPLAY "AREA-LINK INP: " AREA-LINK
           .
      ******************************************************************
      * 0200-EJECUTAR-OPERACION
      ******************************************************************
       0200-EJECUTAR-OPERACION.
      *    DISPLAY "LK-PF-COD-OPE: " LK-PF-TIPO-OPE
           EVALUATE LK-PF-TIPO-OPE
              WHEN 1
      *          DISPLAY "CONSULTA"
                 PERFORM 0210-CONSULTAR-PERFIL
              WHEN 2
      *          DISPLAY "ADICION"
                 PERFORM 0220-INSERTAR-PERFIL
              WHEN 3
      *          DISPLAY "DELETE"
                 PERFORM 0230-BORRAR-PERFIL
              WHEN 4
      *          DISPLAY "UPDATE"
                 PERFORM 0240-MODIFICAR-PERFIL
              WHEN OTHER
      *          DISPLAY "OPCION NO IMPLEMENTADA"
                 MOVE 9                        TO LK-PF-COD-RET
           END-EVALUATE
           .
      **************************************************************
      * 0210-CONSULTAR-PERFIL
      **************************************************************
       0210-CONSULTAR-PERFIL.
           MOVE 1              TO LK-PF-COD-RET
           MOVE LK-PF-TRAN     TO PF-TRAN
           MOVE LK-PF-IDPERFIL TO PF-IDPERFIL
      *
           EXEC SQL
             SELECT
                NOMBRE, CONSUL, BORRAR, INGRESAR, MODIF
             INTO
                :PF-NOMBRE, :PF-CONSUL, :PF-BORRAR,
                :PF-INGRESAR, :PF-MODIF
             FROM PERFILES
             WHERE TRAN = :PF-TRAN  AND  IDPERFIL = :PF-IDPERFIL
           END-EXEC

           IF SQLCODE = 0 THEN
              MOVE 0             TO  LK-PF-COD-RET
              MOVE PF-NOMBRE     TO  LK-PF-NOMBRE
              MOVE PF-CONSUL     TO  LK-PF-CONSUL
              MOVE PF-BORRAR     TO  LK-PF-BORRAR
              MOVE PF-INGRESAR   TO  LK-PF-INGRESAR
              MOVE PF-MODIF      TO  LK-PF-MODIF
           ELSE
              IF SQLCODE = +100 THEN
                 MOVE 1 TO  LK-PF-COD-RET
              ELSE
                 MOVE 2 TO  LK-PF-COD-RET
                 PERFORM 0900-ERROR-DB2
              END-IF
           END-IF
           .
      ***************************************************
      * INSERTA LOS DATOS EN LA TABLA                   *
      ***************************************************
       0220-INSERTAR-PERFIL.
           MOVE LK-PF-TRAN       TO  PF-TRAN
           MOVE LK-PF-IDPERFIL   TO  PF-IDPERFIL
           MOVE LK-PF-NOMBRE     TO  PF-NOMBRE
           MOVE LK-PF-CONSUL     TO  PF-CONSUL
           MOVE LK-PF-BORRAR     TO  PF-BORRAR
           MOVE LK-PF-INGRESAR   TO  PF-INGRESAR
           MOVE LK-PF-MODIF      TO  PF-MODIF

           EXEC SQL
             INSERT INTO PERFILES
                (
                 TRAN, IDPERFIL, NOMBRE, CONSUL, BORRAR,
                 INGRESAR, MODIF
                )
             VALUES
                (
                 :PF-TRAN, :PF-IDPERFIL, :PF-NOMBRE, :PF-CONSUL,
                 :PF-BORRAR, :PF-INGRESAR, :PF-MODIF
                )
           END-EXEC

           IF SQLCODE = 0
             MOVE 0         TO LK-PF-COD-RET
           ELSE
             IF SQLCODE = +100 THEN
                MOVE 1      TO  LK-PF-COD-RET
             ELSE
                MOVE 2      TO  LK-PF-COD-RET
                PERFORM 0900-ERROR-DB2
             END-IF
           END-IF
           .
      ***************************************************
      * BORRA UN REGISTRO CON EL NUMERO DE LA CLAVE     *
      ***************************************************
       0230-BORRAR-PERFIL.
           MOVE LK-PF-TRAN       TO  PF-TRAN
           MOVE LK-PF-IDPERFIL   TO  PF-IDPERFIL

           EXEC SQL
             DELETE FROM PERFILES
               WHERE TRAN = :PF-TRAN  AND  IDPERFIL = :PF-IDPERFIL
           END-EXEC

           IF SQLCODE = 0
             MOVE 0         TO LK-PF-COD-RET
           ELSE
             IF SQLCODE = +100 THEN
                MOVE 1      TO  LK-PF-COD-RET
             ELSE
                MOVE 2      TO  LK-PF-COD-RET
                PERFORM 0900-ERROR-DB2
             END-IF
           END-IF
           .
      ***************************************************
      * ACTUALIZA LOS DATOS EN LA TABLA                 *
      ***************************************************
       0240-MODIFICAR-PERFIL.
           MOVE LK-PF-TRAN       TO  PF-TRAN
           MOVE LK-PF-IDPERFIL   TO  PF-IDPERFIL
           MOVE LK-PF-NOMBRE     TO  PF-NOMBRE
           MOVE LK-PF-CONSUL     TO  PF-CONSUL
           MOVE LK-PF-BORRAR     TO  PF-BORRAR
           MOVE LK-PF-INGRESAR   TO  PF-INGRESAR
           MOVE LK-PF-MODIF      TO  PF-MODIF

           EXEC SQL
              UPDATE PERFILES
              SET NOMBRE      = :PF-NOMBRE,
                  CONSUL      = :PF-CONSUL,
                  BORRAR      = :PF-BORRAR,
                  INGRESAR    = :PF-INGRESAR,
                  MODIF       = :PF-MODIF
              WHERE TRAN = :PF-TRAN  AND  IDPERFIL = :PF-IDPERFIL
           END-EXEC

           IF SQLCODE = 0
             MOVE 0         TO LK-PF-COD-RET
           ELSE
             IF SQLCODE = +100 THEN
                MOVE 1      TO  LK-PF-COD-RET
             ELSE
                MOVE 2      TO  LK-PF-COD-RET
                PERFORM 0900-ERROR-DB2
             END-IF
           END-IF
           .
      ******************************************************************
      * 0900-ERROR-DB2
      ******************************************************************
       0900-ERROR-DB2.
           MOVE SQLCODE     TO LK-PF-SQLCODE
           MOVE SQLCODE     TO DB2-SQLCODE
      *    DISPLAY "ERROR DB2 EN RUTINA: "
      *    DISPLAY "DB2-SQLCODE ----> "  DB2-SQLCODE
      *    DISPLAY "DB2-SQLCODE-Z --> "  DB2-SQLCODE-Z
      *    DISPLAY "DB2-ERR-MSG ----> "  DB2-ERR-MSG
      *    DISPLAY "DB2-ERR-CODE ---> "  DB2-ERR-CODE
           .
      ******************************************************************
      * FIN SESION
      ******************************************************************
       0300-FIN.
      *    DISPLAY "AREA-LINK OUT: " AREA-LINK
      *    DISPLAY "SALGO DE RUTINA RTSNOTPF"
           GOBACK
           .
      ******************************************************************
