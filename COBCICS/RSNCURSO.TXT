      *************************************************************
      *   ENCAPSULADOR TABLA CURSOS
      *   RUTINA QUE EJECUTA LAS SENTENCIAS DE LA BASE DE DATOS A
      *   PARTIR DE UN TIPO Y SUBTIPO DE OPERACION QUE ENVIA
      *   EL PROGRAMA QUE LA INVOCA.
      *   TIPO 1: CONSULTA - SELECT
      *      SUBTIPO 1: CONSULTA POR CURSO
      *        2: ADICION - INSERT
      *        3: BORRAR - DELETE
      *        4: MODIFICAR - UPDATE
      *************************************************************
      * MODIFICACIONES                                            *
      * OCT 9-2024 : ULTIMA VERSION DE LA ACTIVIDAD DEL SISTEMA   *
      * SAUL         DE NOTAS                                     *
      *              SE AGREGO LA TERMINAL A LOS NOMBRES DE LAS   *
      *              COLAS PARA QUE FUNCIONEN BIEN EN SERVIDORES  *
      *************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. RSNCURSO.
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
      *===============================================================
       01  AREA-LINK.
           05 LK-CR-TIPO-OPE     PIC 9(01).
           05 LK-CR-SUBTIPO-OPE  PIC 9(02).
           05 LK-CR-CURSO        PIC X(05).
           05 LK-CR-DESCRIPCION  PIC X(20).
           05 LK-CR-DNIPROFESOR  PIC X(09).
           05 LK-CR-ESTADO       PIC X(01).
           05 LK-CR-COD-RET      PIC 9(02).
           05 LK-CR-SQLCODE      PIC S9(9).
       01 COLA-CURSOS.
          05 C-CURSO       PIC X(05).
          05 C-DESCRIPCION PIC X(20).
          05 C-DNIPROFESOR PIC X(09).
       01 WS-COLA-CURSOS PIC X(08) .
       01 RESPUESTA      PIC S9(08) COMP.
       01 WS-EOF         PIC X VALUE SPACES.
       01 WS-COLATRABAJO PIC X(08) VALUE 'KCICS01'.
       01 COLA-TRABAJO.
          05 T-AREA-LINK1 PIC X(01).
          05 T-AREA-LINKF1 PIC XX.
          05 T-AREA-LINK2 PIC X(09).
          05 T-AREA-LINKF2 PIC XX.
          05 T-AREA-LINK3 PIC X(100).
          05 T-DNI       PIC X(09).
          05 T-PAS       PIC X(08).
          05 T-CLAVE     PIC X(08).
          05 T-NOMBRE    PIC X(20).
          05 T-APELLIDOS PIC X(20).
          05 T-DIRECCION PIC X(35).
          05 T-SQLCODE   PIC X(10).
      * MODSQL - DEFINICION TABLA CURSOS SQL Y COBOL
           EXEC SQL
                INCLUDE CURSOS
           END-EXEC.
      *===============================================================
      * MODSQL - SE TIENE QUE UTILIZAR SQLCA
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.
      *===============================================================
           EXEC SQL
              DECLARE CSR1 CURSOR FOR
                   SELECT
                        CURSO
                       ,DESCRIPCION
                       ,DNIPROFESOR
                   FROM
                       CURSOS
                   WHERE
                       DNIPROFESOR = :LK-CR-DNIPROFESOR
                   ORDER BY CURSO ASC
           END-EXEC
      * MODSQL CAMPOS PARA ERROR DE DB2
       01  DB2-ERR.
           03  DB2-SQLCODE                PIC S9(9).
           03  DB2-SQLCODE-Z              PIC -ZZZZZZZZ9.
           03  DB2-ERROR.
               05  DB2-ERR-MSG            PIC X(50).
               05  DB2-ERR-CODE           PIC X(20).

      *============================================================
       LINKAGE SECTION.
       01  DFHCOMMAREA        PIC X(49).

      *=================================================================
       PROCEDURE DIVISION USING DFHCOMMAREA.
      *=================================================================
      ******************************************************************
      * 0000-RAIZ
      ******************************************************************
       0000-RAIZ.
      *    MODSQL EXCEPCIONES SQL DB2
           EXEC SQL
                WHENEVER  SQLERROR    CONTINUE
           END-EXEC

           EXEC SQL
                WHENEVER  SQLWARNING  CONTINUE
           END-EXEC

           EXEC SQL
                WHENEVER  NOT FOUND   CONTINUE
           END-EXEC

           PERFORM 0100-INICIO
           PERFORM 0200-EJECUTAR-OPERACION
           PERFORM 0300-FIN
           .
      ******************************************************************
      * 0100-INICIO
      ******************************************************************
       0100-INICIO.
           MOVE DFHCOMMAREA TO AREA-LINK
           MOVE 'KSNC'        TO WS-COLA-CURSOS(1:4)
           MOVE EIBTRMID      TO WS-COLA-CURSOS(5:4)
           .
      ******************************************************************
      * 0200-EJECUTAR-OPERACION
      ******************************************************************
       0200-EJECUTAR-OPERACION.
           EVALUATE LK-CR-TIPO-OPE
              WHEN '1'
                 PERFORM 0210-CONSULTAR-CURSO
              WHEN '2'
                 PERFORM 0220-INSERTAR-CURSO
              WHEN '3'
                 PERFORM 0230-BORRAR-CURSO
              WHEN '4'
                 PERFORM 0240-MODIFICAR-CURSO
              WHEN '5'
                 IF LK-CR-SUBTIPO-OPE = '01'
                   PERFORM 0250-CURSOR-01
                 END-IF
              WHEN OTHER
                 MOVE 9             TO LK-CR-COD-RET
           END-EVALUATE
           .
      **************************************************************
      * 0210-CONSULTAR-CURSO
      **************************************************************
       0210-CONSULTAR-CURSO.
           MOVE 1           TO LK-CR-COD-RET
           MOVE LK-CR-CURSO TO CR-CURSO
      *
           EXEC SQL
             SELECT
                DESCRIPCION, DNIPROFESOR, ESTADO
             INTO
                :CR-DESCRIPCION, :CR-DNIPROFESOR, :CR-ESTADO
             FROM CURSOS
             WHERE CURSO =:CR-CURSO
           END-EXEC

           IF SQLCODE = 0 THEN
              MOVE 0               TO  LK-CR-COD-RET
              MOVE CR-DESCRIPCION  TO  LK-CR-DESCRIPCION
              MOVE CR-DNIPROFESOR TO   LK-CR-DNIPROFESOR
              MOVE CR-ESTADO       TO  LK-CR-ESTADO
           ELSE
              IF SQLCODE = +100 THEN
                 MOVE 1 TO  LK-CR-COD-RET
              ELSE
                 MOVE 2 TO  LK-CR-COD-RET
                 PERFORM 0900-ERROR-DB2
              END-IF
           END-IF
           .
      ***************************************************
      * INSERTA LOS DATOS EN LA TABLA                   *
      ***************************************************
       0220-INSERTAR-CURSO.
           MOVE LK-CR-CURSO        TO  CR-CURSO
           MOVE LK-CR-DESCRIPCION  TO  CR-DESCRIPCION
           MOVE LK-CR-DNIPROFESOR TO   CR-DNIPROFESOR
           MOVE LK-CR-ESTADO       TO  CR-ESTADO

           EXEC SQL
             INSERT INTO CURSOS
                (
                 CURSO, DESCRIPCION, DNIPROFESOR, ESTADO
                )
             VALUES
                (
                 :CR-CURSO,:CR-DESCRIPCION, :CR-DNIPROFESOR,  :CR-ESTADO
                )
           END-EXEC

           IF SQLCODE = 0
             MOVE 0         TO LK-CR-COD-RET
           ELSE
             IF SQLCODE = +100 THEN
                MOVE 1      TO  LK-CR-COD-RET
             ELSE
                MOVE 2      TO  LK-CR-COD-RET
                PERFORM 0900-ERROR-DB2
             END-IF
           END-IF
           .
      ***************************************************
      * BORRA UN REGISTRO CON EL NUMERO DE LA CLAVE     *
      ***************************************************
       0230-BORRAR-CURSO.
           MOVE LK-CR-CURSO      TO  CR-CURSO

           EXEC SQL
             DELETE FROM CURSOS
               WHERE CURSO =:CR-CURSO
           END-EXEC

           IF SQLCODE = 0
             MOVE 0         TO LK-CR-COD-RET
           ELSE
             IF SQLCODE = +100 THEN
                MOVE 1      TO  LK-CR-COD-RET
             ELSE
                MOVE 2      TO  LK-CR-COD-RET
                PERFORM 0900-ERROR-DB2
             END-IF
           END-IF
           .
      ***************************************************
      * ACTUALIZA LOS DATOS EN LA TABLA                 *
      ***************************************************
       0240-MODIFICAR-CURSO.
           MOVE LK-CR-CURSO        TO  CR-CURSO
           MOVE LK-CR-DESCRIPCION  TO  CR-DESCRIPCION
           MOVE LK-CR-DNIPROFESOR TO   CR-DNIPROFESOR
           MOVE LK-CR-ESTADO       TO  CR-ESTADO

           EXEC SQL
              UPDATE CURSOS
              SET CURSO        = :CR-CURSO,
                  DESCRIPCION  = :CR-DESCRIPCION,
                  DNIPROFESOR = :CR-DNIPROFESOR,
                  ESTADO       = :CR-ESTADO
              WHERE CURSO      = :CR-CURSO
           END-EXEC

           IF SQLCODE = 0
             MOVE 0         TO LK-CR-COD-RET
           ELSE
             IF SQLCODE = +100 THEN
                MOVE 1      TO  LK-CR-COD-RET
             ELSE
                MOVE 2      TO  LK-CR-COD-RET
                PERFORM 0900-ERROR-DB2
             END-IF
           END-IF
           .
       0250-CURSOR-01.
           EXEC CICS DELETEQ TS
                QUEUE (WS-COLA-CURSOS)
                RESP (RESPUESTA)
           END-EXEC
           EXEC SQL
                OPEN CSR1
           END-EXEC
           IF SQLCODE EQUAL ZERO
            PERFORM 0263-FECTCH-CURSOS-DETA01
                UNTIL SQLCODE NOT EQUAL 0
           END-IF

           EXEC SQL
                CLOSE CSR1
           END-EXEC
           .
       0263-FECTCH-CURSOS-DETA01.
           EXEC SQL
           FETCH CSR1
                   INTO
                       :CR-CURSO
                      ,:CR-DESCRIPCION
                      ,:CR-DNIPROFESOR
           END-EXEC
           IF SQLCODE = +100
            MOVE 'Y' TO WS-EOF
           END-IF
           IF SQLCODE = 0
             MOVE CR-CURSO       TO C-CURSO
             MOVE CR-DESCRIPCION TO C-DESCRIPCION
             MOVE CR-DNIPROFESOR TO C-DNIPROFESOR
             EXEC CICS WRITEQ TS
                QUEUE (WS-COLA-CURSOS)
                FROM (COLA-CURSOS)
             END-EXEC
             .
      ******************************************************************
      * 0900-ERROR-DB2
      ******************************************************************
       0900-ERROR-DB2.
           MOVE SQLCODE TO LK-CR-SQLCODE
           MOVE SQLCODE TO DB2-SQLCODE
      *    DISPLAY "ERROR DB2 EN RUTINA: "
      *    DISPLAY "DB2-SQLCODE ----> "  DB2-SQLCODE
      *    DISPLAY "DB2-SQLCODE-Z --> "  DB2-SQLCODE-Z
      *    DISPLAY "DB2-ERR-MSG ----> "  DB2-ERR-MSG
      *    DISPLAY "DB2-ERR-CODE ---> "  DB2-ERR-CODE
           .
      ******************************************************************
      * FIN SESION
      ******************************************************************
       0300-FIN.
           MOVE AREA-LINK TO DFHCOMMAREA
           GOBACK
           .
      ******************************************************************
