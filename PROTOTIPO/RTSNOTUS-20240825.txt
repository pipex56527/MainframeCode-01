      *************************************************************
      *   ENCAPSULADOR TABLA USUARIOS
      *************************************************************
       IDENTIFICATION DIVISION.
         PROGRAM-ID. RTSNOTUS.
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
      *===============================================================
      * MODSQL - DEFINICON TABLA USUARIOS SQL Y COBOL
           EXEC SQL
                INCLUDE USUARIOS
           END-EXEC.
      *===============================================================
      * MODSQL - SE TIENE QUE UTILIZAR SQLCA
           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.
      *===============================================================
      * MODSQL CAMPOS PARA ERROR DE DB2
       01  DB2-ERR.
           03  DB2-SQLCODE                PIC S9(9).
           03  DB2-SQLCODE-Z              PIC -ZZZZZZZZ9.
           03  DB2-ERROR.
               05  DB2-ERR-MSG            PIC X(50).
               05  DB2-ERR-CODE           PIC X(20).

      *============================================================
       LINKAGE SECTION.
       01  DFHCOMMAREA        PIC X(120).
       01  AREA-LINK          REDEFINES DFHCOMMAREA.
           05 WS-COD-RET      PIC 9(01).
           05 WS-COD-OPE      PIC 9(01).
           05 WS-US-DNI       PIC X(09).
           05 WS-US-TIPOUSUA  PIC X(05).
           05 WS-US-CLAVE     PIC X(08).
           05 WS-US-NOMBRE    PIC X(20).
           05 WS-US-APELLIDOS PIC X(20).
           05 WS-US-DIRECCION PIC X(35).
           05 WS-US-CARGO     PIC X(20).
           05 WS-US-ESTADO    PIC X(01).

      *=================================================================
       PROCEDURE DIVISION USING DFHCOMMAREA.
      *=================================================================
      ******************************************************************
      * 0000-RAIZ
      ******************************************************************
       0000-RAIZ.
      *    MODSQL EXCEPCIONES SQL DB2
           EXEC SQL
                WHENEVER  SQLERROR    CONTINUE
           END-EXEC

           EXEC SQL
                WHENEVER  SQLWARNING  CONTINUE
           END-EXEC

           EXEC SQL
                WHENEVER  NOT FOUND   CONTINUE
           END-EXEC

           PERFORM 0100-INICIO
           PERFORM 0200-EJECUTAR-OPERACION
           PERFORM 0300-FIN
           .
      ******************************************************************
      * 0100-INICIO
      ******************************************************************
       0100-INICIO.
           DISPLAY "ENTRO A RUTINA RTSNOTUS"
           DISPLAY "AREA-LINK INP: " AREA-LINK
           .
      ******************************************************************
      * 0200-EJECUTAR-OPERACION
      ******************************************************************
       0200-EJECUTAR-OPERACION.
           DISPLAY "WS-COD-OPE: " WS-COD-OPE
           EVALUATE WS-COD-OPE
              WHEN 1
      *          DISPLAY "CONSULTA"
                 PERFORM 0210-CONSULTAR-USUARIO
              WHEN 2
      *          DISPLAY "ADICION"
                 PERFORM 0220-INSERTAR-USUARIO
              WHEN 3
      *          DISPLAY "DELETE"
                 PERFORM 0230-BORRAR-USUARIO
              WHEN 4
      *          DISPLAY "UPDATE"
                 PERFORM 0240-MODIFICAR-USUARIO
              WHEN 5
      *          DISPLAY "ANTERIOR"
                 PERFORM 0250-TIPO-USER-PREV
              WHEN 6
      *          DISPLAY "SIGUIENTE"
                 PERFORM 0260-TIPO-USER-NEXT
              WHEN OTHER
                 DISPLAY "OPCION NO IMPLEMENTADA"
                 MOVE 9                        TO WS-COD-RET
           END-EVALUATE
           .
      **************************************************************
      * 0210-CONSULTAR-USUARIO
      **************************************************************
       0210-CONSULTAR-USUARIO.
           MOVE 1           TO WS-COD-RET
           MOVE WS-US-DNI   TO US-DNI
      *
           EXEC SQL
             SELECT
                TIPOUSUA,  CLAVE, NOMBRE, APELLIDOS,
                DIRECCION, CARGO, ESTADO
             INTO
                :US-TIPOUSUA, :US-CLAVE, :US-NOMBRE, :US-APELLIDOS,
                :US-DIRECCION, :US-CARGO, :US-ESTADO
             FROM USUARIOS
             WHERE DNI = :US-DNI
           END-EXEC

           IF SQLCODE = 0 THEN
              MOVE 0             TO  WS-COD-RET
              MOVE US-TIPOUSUA   TO  WS-US-TIPOUSUA
              MOVE US-CLAVE      TO  WS-US-CLAVE
              MOVE US-NOMBRE     TO  WS-US-NOMBRE
              MOVE US-APELLIDOS  TO  WS-US-APELLIDOS
              MOVE US-DIRECCION  TO  WS-US-DIRECCION
              MOVE US-CARGO      TO  WS-US-CARGO
              MOVE US-ESTADO     TO  WS-US-ESTADO
           ELSE
              IF SQLCODE = +100 THEN
                 MOVE 1 TO  WS-COD-RET
              ELSE
                 MOVE 2 TO  WS-COD-RET
                 PERFORM ERROR-DB2
              END-IF
           END-IF
           .
      ***************************************************
      * INSERTA LOS DATOS EN LA TABLA                   *
      ***************************************************
       0220-INSERTAR-USUARIO.
           MOVE WS-US-DNI        TO  US-DNI
           MOVE WS-US-TIPOUSUA   TO  US-TIPOUSUA
           MOVE WS-US-CLAVE      TO  US-CLAVE
           MOVE WS-US-NOMBRE     TO  US-NOMBRE
           MOVE WS-US-APELLIDOS  TO  US-APELLIDOS
           MOVE WS-US-DIRECCION  TO  US-DIRECCION
           MOVE WS-US-CARGO      TO  US-CARGO
           MOVE WS-US-ESTADO     TO  US-ESTADO

           EXEC SQL
             INSERT INTO USUARIOS
                (
                 DNI, TIPOUSUA, CLAVE, NOMBRE,
                 APELLIDOS, DIRECCION, CARGO, ESTADO
                )
             VALUES
                (
                 :US-DNI, :US-TIPOUSUA, :US-CLAVE, :US-NOMBRE,
                 :US-APELLIDOS, :US-DIRECCION, :US-CARGO, :US-ESTADO
                )
           END-EXEC

           IF SQLCODE = 0
             MOVE 0                TO WS-COD-RET
           ELSE
              IF SQLCODE = +100 THEN
                 MOVE 1 TO  WS-COD-RET
              ELSE
                 MOVE 2 TO  WS-COD-RET
                 PERFORM ERROR-DB2
              END-IF
           END-IF
           .
      ***************************************************
      * BORRA UN REGISTRO CON EL NUMERO DE LA CLAVE     *
      ***************************************************
       0230-BORRAR-USUARIO.
           MOVE WS-US-DNI        TO  US-DNI
           EXEC SQL
             DELETE FROM USUARIOS
                    WHERE DNI = :US-DNI
           END-EXEC

           IF SQLCODE = 0
             MOVE 0                TO WS-COD-RET
           ELSE
              IF SQLCODE = +100 THEN
                 MOVE 1 TO  WS-COD-RET
              ELSE
                 MOVE 2 TO  WS-COD-RET
                 PERFORM ERROR-DB2
              END-IF
           END-IF
           .
      ***************************************************
      * ACTUALIZA LOS DATOS EN LA TABLA                 *
      ***************************************************
       0240-MODIFICAR-USUARIO.
           MOVE WS-US-TIPOUSUA   TO  US-TIPOUSUA
           MOVE WS-US-CLAVE      TO  US-CLAVE
           MOVE WS-US-NOMBRE     TO  US-NOMBRE
           MOVE WS-US-APELLIDOS  TO  US-APELLIDOS
           MOVE WS-US-DIRECCION  TO  US-DIRECCION
           MOVE WS-US-CARGO      TO  US-CARGO
           MOVE WS-US-ESTADO     TO  US-ESTADO
           EXEC SQL
              UPDATE USUARIOS
              SET NOMBRE    = :US-NOMBRE,
                  TIPOUSUA  = :US-TIPOUSUA,
                  CLAVE     = :US-CLAVE,
                  NOMBRE    = :US-NOMBRE,
                  APELLIDOS = :US-APELLIDOS,
                  DIRECCION = :US-DIRECCION,
                  CARGO     = :US-CARGO,
                  ESTADO    = :US-ESTADO
              WHERE DNI = :US-DNI
           END-EXEC

           IF SQLCODE = 0
             MOVE 0                TO WS-COD-RET
           ELSE
              IF SQLCODE = +100 THEN
                 MOVE 1 TO  WS-COD-RET
              ELSE
                 MOVE 2 TO  WS-COD-RET
                 PERFORM ERROR-DB2
              END-IF
           END-IF
           .
      ***************************************************
      * 0250-TIPO-USER-PREV                        *
      ***************************************************
       0250-TIPO-USER-PREV.
           MOVE WS-US-DNI        TO US-DNI
           MOVE WS-US-TIPOUSUA   TO US-TIPOUSUA

           EXEC SQL
                DECLARE CURPREV CURSOR FOR
                   SELECT
                      DNI, TIPOUSUA,  CLAVE, NOMBRE,
                      APELLIDOS, DIRECCION, CARGO, ESTADO
                   FROM USUARIOS
                   WHERE TIPOUSUA = :US-TIPOUSUA
                     AND DNI < :US-DNI
                   ORDER BY DNI DESC
           END-EXEC
      *
           EXEC SQL
                OPEN CURPREV
           END-EXEC
      *
           EXEC SQL
                FETCH CURPREV
                INTO
                     :US-DNI
                   , :US-TIPOUSUA
                   , :US-CLAVE
                   , :US-NOMBRE
                   , :US-APELLIDOS
                   , :US-DIRECCION
                   , :US-CARGO
                   , :US-ESTADO
           END-EXEC
      *
           IF SQLCODE = 0 THEN
      *       REGISTRO ANTERIOR
              MOVE 0                      TO WS-COD-RET
              MOVE US-NOMBRE              TO WS-US-NOMBRE
              MOVE US-APELLIDOS           TO WS-US-APELLIDOS
              MOVE US-DIRECCION           TO WS-US-DIRECCION
              MOVE US-CARGO               TO WS-US-CARGO
              MOVE US-ESTADO              TO WS-US-ESTADO
           ELSE
              IF SQLCODE = +100 THEN
                 MOVE 1             TO  WS-COD-RET
              ELSE
                 MOVE 2             TO  WS-COD-RET
                 PERFORM ERROR-DB2
              END-IF
           END-IF

           EXEC SQL
                CLOSE CURPREV
           END-EXEC
           .
      ***************************************************
      * 0260-TIPO-USER-NEXT                             *
      ***************************************************
       0260-TIPO-USER-NEXT.
           MOVE WS-US-DNI        TO US-DNI
           MOVE WS-US-TIPOUSUA   TO US-TIPOUSUA

           EXEC SQL
                DECLARE CURNEXT CURSOR FOR
                   SELECT
                      DNI, TIPOUSUA,  CLAVE, NOMBRE,
                      APELLIDOS, DIRECCION, CARGO, ESTADO
                   FROM USUARIOS
                   WHERE TIPOUSUA = :US-TIPOUSUA
                     AND DNI > :US-DNI
                   ORDER BY DNI ASC
           END-EXEC
      *
           EXEC SQL
                OPEN CURNEXT
           END-EXEC
      *
           EXEC SQL
                FETCH CURNEXT
                INTO
                     :US-DNI
                   , :US-TIPOUSUA
                   , :US-CLAVE
                   , :US-NOMBRE
                   , :US-APELLIDOS
                   , :US-DIRECCION
                   , :US-CARGO
                   , :US-ESTADO
           END-EXEC
      *
           IF SQLCODE = 0 THEN
      *       REGISTRO SIGUIENTE
              MOVE 0                      TO WS-COD-RET
              MOVE US-NOMBRE              TO WS-US-NOMBRE
              MOVE US-APELLIDOS           TO WS-US-APELLIDOS
              MOVE US-DIRECCION           TO WS-US-DIRECCION
              MOVE US-CARGO               TO WS-US-CARGO
              MOVE US-ESTADO              TO WS-US-ESTADO
           ELSE
              IF SQLCODE = +100 THEN
                 MOVE 1             TO  WS-COD-RET
              ELSE
                 MOVE 2             TO  WS-COD-RET
                 PERFORM ERROR-DB2
              END-IF
           END-IF

           EXEC SQL
                CLOSE CURNEXT
           END-EXEC
           .
      ******************************************************************
      * ERROR-DB2
      ******************************************************************
       ERROR-DB2.
           DISPLAY "ERROR DB2 EN RUTINA: "
           DISPLAY "DB2-SQLCODE ----> "  DB2-SQLCODE
           DISPLAY "DB2-SQLCODE-Z --> "  DB2-SQLCODE-Z
           DISPLAY "DB2-ERR-MSG ----> "  DB2-ERR-MSG
           DISPLAY "DB2-ERR-CODE ---> "  DB2-ERR-CODE
           .
      ******************************************************************
      * FIN SESION
      ******************************************************************
       0300-FIN.
           DISPLAY "AREA-LINK OUT: " AREA-LINK
           DISPLAY "SALGO DE RUTINA RTSNOTUS"
           GOBACK
           .
      ******************************************************************
